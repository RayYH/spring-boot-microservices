plugins {
  id "io.spring.dependency-management"
  id "checkstyle"
}


apply from: 'lib.gradle'

allprojects {
  group = 'com.rayyounghong.sbms'
  
  repositories {
    mavenCentral()
    maven { url = uri("https://jcenter.bintray.com") }
  }
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'io.spring.dependency-management'
  apply plugin: "checkstyle"
  
  dependencyManagement {
    imports {
      // noinspection ALL
      mavenBom("org.springframework.boot:spring-boot-dependencies:${spring_boot_version}")
      // noinspection ALL
      mavenBom("org.springframework.cloud:spring-cloud-dependencies:${spring_cloud_version}")
      // noinspection ALL
      mavenBom("de.codecentric:spring-boot-admin-dependencies:${spring_boot_admin_version}")
      // noinspection ALL
      mavenBom("io.grpc:grpc-bom:${io_grpc_version}")
      // noinspection ALL
      mavenBom("com.google.protobuf:protobuf-bom:${protobuf_java_version}")
      // noinspection ALL
      mavenBom("org.testcontainers:testcontainers-bom:${org_testcontainers_bom}")
    }
  }
  
  configurations.configureEach {
    resolutionStrategy {
      // don't cache changing modules at all
      // noinspection ALL
      cacheChangingModulesFor 0, 'seconds'
    }
  }
  
  dependencies {
    // noinspection ALL
    implementation rootProject.ext.libraries['cloud-starter-sleuth']
    // noinspection ALL
    implementation rootProject.ext.libraries['cloud-starter-sleuth-zipkin']
    // noinspection ALL
    implementation rootProject.ext.libraries['boot-starter-actuator']
    // noinspection ALL
    implementation rootProject.ext.libraries['micrometer-tracing-bridge-brave']
    // noinspection ALL
    implementation rootProject.ext.libraries['zipkin-reporter-brave']
    // noinspection ALL
    runtimeOnly rootProject.ext.libraries['prometheus']
  }
}

tasks.register('showRepos') {
  def repositoryNames = repositories.collect { it.name }
  doLast {
    println "All repos:"
    println repositoryNames
  }
}